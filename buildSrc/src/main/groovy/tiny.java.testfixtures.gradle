plugins {
    id 'tiny.java.common'
    id 'tiny.license'
    id "java-test-fixtures"
}

dependencies {
    testFixturesImplementation(
        libs.bundles.junit.jupiter.implementation,
        libs.assertj.core,
        libs.mockito.core
    )
}

tasks.register('addTestFixturesLicense', Copy) {
    if(needLicenceFile(project, 'testFixtures', "${projectDir}/src/testFixtures/")) {
        from(project.parent.file('LICENSE'))
        into("${projectDir}/src/testFixtures/resources/META-INF")
    }
}

private static boolean needLicenceFile(Project project, sourceSetName, sourceSetDir) {
    def result = false
    project.sourceSets.each {
        result |= it.name == sourceSetName && new File(sourceSetDir).exists()
    }
    return result
}

updateLicenses {
    dependsOn addTestFixturesLicense
}

updateLicenseTestFixtures {
    dependsOn addTestFixturesLicense
}
