import java.time.LocalDate

plugins {
    id 'tiny.java.test'
    id "org.cadixdev.licenser"
}

license {
    header = project.parent.file('COPYRIGHT.txt')
    include '**/*.java'
    newLine = false
    style {
        java = 'JAVADOC'
    }
    properties {
        year = LocalDate.now().getYear()
    }
}

tasks.register('addMainLicense', Copy) {
    if(needLicenceFile(project, 'main', "${projectDir}/src/main/")) {
        from(project.parent.file('LICENSE'))
        into("${projectDir}/src/main/resources/META-INF")
    }
}

tasks.register('addTestFixturesLicense', Copy) {
    if(needLicenceFile(project, 'testFixtures', "${projectDir}/src/testFixtures/")) {
        from(project.parent.file('LICENSE'))
        into("${projectDir}/src/testFixtures/resources/META-INF")
    }
}

private static boolean needLicenceFile(Project project, sourceSetName, sourceSetDir) {
    def result = false
    project.sourceSets.each {
        result |= it.name == sourceSetName && new File(sourceSetDir).exists()
    }
    return result
}

updateLicenses {
    dependsOn addMainLicense
    dependsOn addTestFixturesLicense
}


updateLicenseMain {
    dependsOn addMainLicense
}

updateLicenseTestFixtures {
    dependsOn addTestFixturesLicense
}
