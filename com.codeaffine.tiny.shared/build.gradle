plugins {
    id 'java'
    id "java-test-fixtures"
    id 'org.javamodularity.moduleplugin' version '1.8.12' apply true
    id "io.freefair.lombok" version "6.5.1"
    id 'maven-publish'
}

group 'com.codeaffine.tiny'
version '1.0.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    modularity.inferModulePath.set(false)
}

repositories {
    mavenCentral()
}

dependencies {

    compileOnly(
            'org.projectlombok:lombok:1.18.24'
    )
    testImplementation(
            platform('org.junit:junit-bom:5.9.1'),
            'org.junit.jupiter:junit-jupiter',
            'org.assertj:assertj-core:3.23.1',
            'org.mockito:mockito-core:4.8.1',
            testFixtures(project(":com.codeaffine.tiny.test"))
    )
    testFixturesImplementation(
            platform('org.junit:junit-bom:5.9.1'),
            'org.junit.jupiter:junit-jupiter',
            'org.assertj:assertj-core:3.23.1',
            'org.mockito:mockito-core:4.8.1'
    )
    testRuntimeOnly(
            'org.junit.jupiter:junit-jupiter-engine:5.9.1',
    )
}

compileTestJava {
    moduleOptions {
        compileOnClasspath = true
    }
}

test {
    useJUnitPlatform()
    moduleOptions {
        runOnClasspath = true
    }
}

publishing {
    publications {
        tinyShared(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = "tiny"
            url = uri("https://maven.pkg.github.com/fappel/tiny")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
}
