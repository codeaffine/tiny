plugins {
    id 'java'
    id 'org.javamodularity.moduleplugin' version '1.8.12' apply true
    id "io.freefair.lombok" version "6.5.1"
    id 'maven-publish'
    id "org.sonarqube" version "3.5.0.2730"
}

group 'com.codeaffine.tiny'
version '1.0.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {

    compileOnly(
            'org.projectlombok:lombok:1.18.24'
    )
    implementation(
            'org.apache.logging.log4j:log4j-core:2.19.0',
            'org.apache.logging.log4j:log4j-slf4j2-impl:2.19.0',
            'org.apache.logging.log4j:log4j-appserver:2.19.0',
            'org.slf4j:slf4j-api:2.0.3',
            project(':com.codeaffine.tiny.shared'),
            project(':com.codeaffine.tiny.star')
    )
    testImplementation(
            project(':com.codeaffine.tiny.star.tck'),
            'org.junit.jupiter:junit-jupiter-api:5.9.0',
            'org.assertj:assertj-core:3.23.1',
            'org.mockito:mockito-core:4.8.1',
            'org.eclipse.rap:org.eclipse.rap.rwt:3.22.0',
            testFixtures(project(":com.codeaffine.tiny.test"))
    )
    testRuntimeOnly(
            'org.junit.jupiter:junit-jupiter-engine:5.9.1'
    )
}

compileJava {
    moduleOptions {
        addReads = ['org.apache.logging.log4j.slf4j': 'org.apache.logging.log4j']
    }
}

compileTestJava {
    moduleOptions {
        compileOnClasspath = true
    }
}

test {
    useJUnitPlatform()
    moduleOptions {
        runOnClasspath = true
    }
}

publishing {
    publications {
        tinyStarLog4j(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = "tiny"
            url = uri("https://maven.pkg.github.com/fappel/tiny")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "fappel_tiny"
        property "sonar.organization", "code-affine"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}
